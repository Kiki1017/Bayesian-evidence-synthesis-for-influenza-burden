model {
  fluhosp ~ dpois(rfluhosp*FSNpop) ## The total # of flu hosp in FSN; unobserved
  FSNfluhosp ~ dbin(pt[1],fluhosp) ## Observed flu hosp.
  
  rfludeathosh <- rfluhosp*pdeath*posh/(1-posh) ## Rate of outside-of-hosp mortality
  fludeathosh ~ dpois(rfludeathosh*FSNpop) ## The total # of flu deaths in FSN; unobserved
  
  FSNfludeath ~ dbin(pdeath*pt[2],fluhosp) ## Observed deaths among hospitalized influenza patents
  
  fludeath <- FSNfludeath + fludeathosh  ## total flu deaths
  
  rfluhosp ~ dunif(0,10)
  pdeath ~ dunif(0,1)
  
  
  for (k in 1:2){
    ## The three sensitivities, test probs and flu risks correspond to the 3 diff. tests;
    ## the 4th flu risk and 'test prob' represents those not tested
    
    pt[k] <- (pflu[k,1]*ptest[k,1]*sens[k,1] + pflu[k,2]*ptest[k,2]*sens[k,2] + pflu[k,3]*ptest[k,3]*sens[k,k])/
      (pflu[k,1]*ptest[k,1] + pflu[k,2]*ptest[k,2] + pflu[k,3]*ptest[k,3] + pflu[k,4]*ptest[k,4])
    
    nttype[k,] ~ dmulti(ptest[k,1:4],ntot[k])## observed testing freqs
    ## priors for testing 
    ptest10[k] ~ dunif(0,1)
    ptest[k,1] <- ptest10[k]
    
    ptest20[k] ~ dunif(0,1)
    ptest[k,2] <- ptest20[k]*(1-ptest[k,1])
    ptest30[k] ~ dunif(0,1)
    ptest[k,3] <- ptest30[k]*(1-ptest[k,1]-ptest[k,2])
    ptest[k,4] <- 1 - ptest[k,1] - ptest[k,2] - ptest[k,3]
    
    for (l in 1:4) {
      pflu[k,l] ~ dunif(0,1)
    }
    
    for (m in 1:3) {
      flupos[k,m] ~ dbin(pflu[k,m],nttype[k,m]) ## actual flu pos/test type, unobserved
      testpos[k,m] ~ dbin(sens1[k],flupos[k,m]) ## observed flu pos/test type
    }
    
    ### generating a sensitvity from the given distribution
    
    sens1[k] ~ dnorm(pcrsens[1],1/(pcrsens[2]*pcrsens[2]))
    sens[k,1] <- sens1[k]
    logsens2[k] ~ dnorm(lrapidsens[1],1/(lrapidsens[2]*lrapidsens[2]))I(,0)
    sens[k,2] <- exp(logsens2[k])
    sens3[k] ~ dunif(0,1)
    sens[k,3] <- sens3[k]
  }
  USfluhosp ~ dpois(rfluhosp*Npop) ## The total # of US flu hosp
  
  USfludeathosh ~ dpois(rfludeathosh*Npop) ## The total # of US flu osh deaths
  USfludeathish ~ dbin(pdeath,USfluhosp) ## The total # of US flu ish deaths
  
  USfludeath <- USfludeathosh + USfludeathish ## Total US flu deaths
}
